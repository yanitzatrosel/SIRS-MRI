;****************************************************************
;
;
; Yanitza Trosel
; 
; SIRS-EPI -  echo planar imaging method
;
;
;
;
;****************************************************************
;
; d0 - TR padding
; d1 - refocusing
; d2 - TE padding
; d3 = PVM_RampTime + PVM_InterGradientWaitTime
; d4 = PVM_RiseTime
; d5 = spoiler
; d7 - TE padding (spin echo)
; d8 = SCON / BLKTR_MAN (Amplifier preblanking)
; d10 = EchoTimeIncrement-PVM_EpiModuleTime 

#include <MRI.include>

preset off

define delay durEpi = {$PVM_EpiModuleTime}
"durEpi = durEpi/1000"

;counter of dummy scans (not acquired; DS set to 0)
define loopcounter lcDs
"lcDs = l0"

define loopcounter c_echo
define loopcounter c_echo2 = {$PVM_NEchoImages}

#include <epiHead.mod>
#include <PrepModulesHead.mod>

INIT_DEVICES
 
#define r3d ACQ_spatial_phase_2
; GRAPPA adjustment?
if(PVM_EpiAdjustMode==2)
{
define list <grad_scalar> r3d
}
else
{
grad_zero_for_gs <r3d>
}

grad_matrix.res

subr TriggerOutStart()

start,  10u

subr TriggerPhase()
subr TriggerOutVolume()
subr Tagging()
;----------------------------------SIRS-----------------------------
;        d16    fq2:f1                 
;	d8     gatepulse 1
        (p1:sp1     ph1):f1   
;	100mp
        (p1:sp1     ph2):f1 
;	d4    grad_ramp{0,0,g4} ; spoiler
;        d4     grad_off  
;----------------------------------start of the main loop ----------
slice, 	5u fq8(receive):f1
    5u 

subr TriggerSlice()
subr TriggerOutSlice()

;----------------------------------preparation modules -------------
subr FovSat()  
subr MagTrans()
subr FatSup()
 ;       d11
        d5	grad_ramp{0, 0, g2}
;----------------------------------slice selection------------------	
	d4 	grad_ramp{0, 0, g0} fq1:f1
        d8	gatepulse 1
	(p0:sp0 	ph0):f1
	d3 	grad_off
;----------------------------------slice rephase--------------------
	d1 	grad_ramp{0, 0, g1 } 	
	d4 	grad_off
if(PVM_SignalType == SignalType_Echo)
{
        d7
	d9	grad_ramp{0, 0, g3}
	d4      grad_ramp{0, 0, g0} gatepulse 1
	(p1:sp1  ph1):f1
        d9	grad_ramp{0, 0, g3}
	d4	grad_off
}
;----------------------------------TE padding-----------------------
	d2
        if "lcDs > 0" goto acqDs    
        "c_echo = c_echo2"
nel,    10u
#include <epi.mod>          
       if "c_echo < 2" goto goOn 
        d10
        "c_echo = c_echo - 1"
        goto nel          
goOn,	10u        
	d0 grad_matrix.inc   
	lo to slice times NSLICES
	grad_matrix.res
       d6	;inter-volume delay 

        "lcDs = lcDs - 1"
        if "lcDs >= 0" goto start      ;dummy scans        

;----------------------------------averaging loop-------------------
        2.5u ipp0
	lo to start times NA
;----------------------------------2d loop--------------------------
	2.5u rpp0 Interleaving.inc

	lo to start times PVM_EpiNShots
        0u Interleaving.res  
;----------------------------------3d loop--------------------------
if( ACQ_dim >= 3 )
{
	r3d.inc
	lo to start times ACQ_size[2]
}
;----------------------------------repetitions loop-------------------
	lo to start times NR

SETUP_GOTO(start)

        goto bye

;---------- "subroutines" -----------------------

; ---- empty acquisition for dummy scans
acqDs,  1u
        "c_echo = c_echo2"
nel2,   10u
#include <epiDum.mod>
        if "c_echo <2" goto goOn 
        d10
        "c_echo = c_echo - 1"
        goto nel2
bye,    1u
exit

ph0 = 0 2
ph1 = 0
ph2 = 2
